/*
 * Copyright 2020 Prathab Murugan
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

plugins {
  id 'org.springframework.boot' version '2.2.6.RELEASE'
  id 'io.spring.dependency-management' version '1.0.9.RELEASE'
  id 'net.researchgate.release' version '2.8.1'
  id 'java'
  id 'jacoco'
}

group = 'com.prathab'
version = '2.0.0'
sourceCompatibility = '8'

configurations {
  developmentOnly
  runtimeClasspath {
    extendsFrom developmentOnly
  }
  compileOnly {
    extendsFrom annotationProcessor
  }
}

repositories {
  mavenCentral()
}

ext {
  set('springCloudVersion', "Hoxton.SR4")
  set('mapstructVersion', "1.3.1.Final")
  set('jwtVersion', "0.11.1")
  set('bouncycastleVersion', "1.65")
  set('openApiVersion', "1.4.1")
}

dependencies {



  // Spring web
  implementation 'org.springframework.boot:spring-boot-starter-web'

  // Spring test
  testImplementation('org.springframework.boot:spring-boot-starter-test') {
    exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
  }

  // Spring security
  implementation 'org.springframework.boot:spring-boot-starter-security'
  testImplementation 'org.springframework.security:spring-security-test'

  // Spring JPA
  implementation 'org.springframework.boot:spring-boot-starter-data-jpa'

  // H2
  runtimeOnly 'com.h2database:h2'

  // Devtools
  developmentOnly 'org.springframework.boot:spring-boot-devtools'

  // Json web token
  implementation "io.jsonwebtoken:jjwt-api:${jwtVersion}"
  runtimeOnly "io.jsonwebtoken:jjwt-impl:${jwtVersion}",
          "org.bouncycastle:bcprov-jdk15on:${bouncycastleVersion}",
          "io.jsonwebtoken:jjwt-jackson:${jwtVersion}"

  // Mapstruct
  compileOnly "org.mapstruct:mapstruct-jdk8:${mapstructVersion}"
  annotationProcessor "org.mapstruct:mapstruct-processor:${mapstructVersion}"
  testImplementation "org.mapstruct:mapstruct-jdk8:${mapstructVersion}"

  // Lombok
  compileOnly 'org.projectlombok:lombok'
  annotationProcessor 'org.projectlombok:lombok'

  // Documentation
  implementation "org.springdoc:springdoc-openapi-ui:${openApiVersion}"
}

dependencyManagement {
  imports {
    mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
  }
}

compileJava {
  // Mapstruct options
  options.annotationProcessorPath = configurations.annotationProcessor
  options.compilerArgs << "-Amapstruct.defaultComponentModel=spring"
  options.compilerArgs << "-Amapstruct.unmappedTargetPolicy=IGNORE"
}

test {
  useJUnitPlatform()
}

// Jacoco
test.finalizedBy jacocoTestReport

jacocoTestReport {
  dependsOn {
    test
  }
  reports {
    xml.enabled false
    csv.enabled false
    html.destination file("${buildDir}/jacoco/jacocoHtml")
  }

  // Keep this until mapstruct team comes up with a better solution
  // https://github.com/mapstruct/mapstruct/issues/1528
  afterEvaluate {
    classDirectories.setFrom(files(classDirectories.files.collect {
      fileTree(dir: it, exclude: ['**/mapper/**', '**/mapper'])
    }))
  }
}